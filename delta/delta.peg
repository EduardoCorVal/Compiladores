// Gramatica del lenguaje Delta

comment = r'[/][/].*|[/][*](.|\n)*?[*][/]'

// \w -> [0-9A-Za-z_]
identifier = r'[a-zA-Z]\w*'

program = statement* expression EOF
statement = declaration / assignment / if / while
declaration = 'var' decl_variable (',' decl_variable)* ';'
decl_variable = identifier
assignment = lhs_variable '=' expression ';'
lhs_variable = identifier
if = 'if' expression block ('else if' expression block)* (r'else' block)?
block = '{' statement* '}'
while = 'while' expression block
expression = comparison ('&&' comparison)*
comparison = additive ((r'(==)' / r'(!=)' / r'(>=)' / r'(>)' / r'(<=)' / r'(<)') additive)*
additive = multiplicative (('+' / '-') multiplicative)*
multiplicative = unary (('*' / '/' / '%') unary)*
unary = ('+' / '-' / '!')* primary
primary = decimal / boolean / parenthesis / rhs_variable / literals
decimal = r'\d+'
literals = binary / octal / hexadecimal
binary = r'#b[01]+' 
octal = r'#o[0-7]+'
hexadecimal = r'#x[0-9a-fA-F]+'
boolean = 'true' / 'false'
// parenthesis = r'[(]' expression r'[)]'
parenthesis = '(' expression ')'
rhs_variable = identifier
